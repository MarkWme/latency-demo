import org.gradle.api.file.DuplicatesStrategy

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'com.example.latency'
    version = "1.0.${System.getenv('BUILD_NUMBER') ?: 'local'}"

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }

    tasks.register('generateSource') {
        def pkgDir = file("src/main/java/com/example/latency/${project.name.replace('-', '')}")
        inputs.property("moduleName", project.name)
        outputs.dir(pkgDir)
        doLast {
            pkgDir.mkdirs()
            def cls = new File(pkgDir, "Hello${project.name.capitalize().replace('-', '')}.java")
            cls.text = """
                package com.example.latency.${project.name.replace('-', '')};

                public class Hello${project.name.capitalize().replace('-', '')} {
                    public static String msg() { return "${project.name}"; }
                }
            """.stripIndent().trim()
        }
    }

    // Ensure both compileJava and sourcesJar see generated sources
    tasks.named('compileJava') {
        dependsOn tasks.named('generateSource')
    }

    tasks.withType(Jar).matching { it.name == 'sourcesJar' }.configureEach {
        dependsOn tasks.named('generateSource')
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }


    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = "target"
                url = uri(findProperty('repoUrl') ?: 'http://localhost:8081/repository/maven-releases/')
                allowInsecureProtocol = true

                credentials {
                    username = findProperty('repoUser')
                    password = findProperty('repoPassword')
                }
            }
        }
    }
}
